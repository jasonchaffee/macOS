#!/usr/bin/env zsh

# Usage: install-tools <type> [failure_log] [manual_log]
# Installs all tools of the given type from tools.conf

BASE_DIR=$( cd $( dirname ${(%):-%x} ) &> /dev/null && pwd )
source ${BASE_DIR}/colors

type=$1
failure_log=$2
manual_log=$3

grep "^${type}:" ${BASE_DIR}/../tools.conf | while read entry; do
    [[ -z "$entry" ]] && continue

    # Capture output to show errors in summary
    tmp_output=/tmp/install_output_$$
    ${BASE_DIR}/install-tool "$entry" </dev/null 2>&1 | tee $tmp_output
    exit_code=${pipestatus[1]}

    clean=${entry%%#*}
    clean=${clean%% *}
    pkg=${clean#*:}

    if [[ $exit_code -eq 1 ]]; then
        echo -e "  ${RED}✗${NC} ${type}: ${BOLD}$pkg${NC}" >> $failure_log
        # Capture last 3 lines of error output (skip the "Installing..." line)
        error_lines=$(grep -v "^Installing\|^$" $tmp_output | tail -3)
        if [[ -n "$error_lines" ]]; then
            echo "$error_lines" | while read line; do
                echo -e "      ${RED}$line${NC}" >> $failure_log
            done
        fi
    elif [[ $exit_code -eq 2 && -n "$manual_log" ]]; then
        echo -e "  ${YELLOW}⚠${NC} ${type}: ${BOLD}$pkg${NC}" >> $manual_log
    fi

    rm -f $tmp_output
done
