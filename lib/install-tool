#!/usr/bin/env zsh

# Usage: install-tool <type>:<package>[@version]
# Types: brew, brew-cask, mise, npm, mas, vscode, cursor, uv, gcloud
# Returns: 0 on success, 1 on failure

BASE_DIR=$( cd $( dirname ${(%):-%x} ) &> /dev/null && pwd )
source ${BASE_DIR}/colors

entry=$1
# Strip comments and whitespace
entry=${entry%%#*}
entry=${entry%% *}
entry=${entry%%	*}

type=${entry%%:*}
package_with_version=${entry#*:}
package=${package_with_version%%@*}
version=${package_with_version#*@}
[[ "$version" == "$package" ]] && version="latest"

case $type in
    brew)
        if command -v brew >/dev/null 2>&1; then
            echo -e "${CYAN}Installing brew package:${NC} ${BOLD}$package${NC}"
            brew install $package
            exit $?
        fi
        ;;
    brew-cask)
        if command -v brew >/dev/null 2>&1; then
            echo -e "${CYAN}Installing brew cask:${NC} ${BOLD}$package${NC}"
            brew install --cask $package
            exit $?
        fi
        ;;
    mise)
        if command -v mise >/dev/null 2>&1; then
            echo -e "${CYAN}Installing mise tool:${NC} ${BOLD}${package}@${version}${NC}"
            # Ensure plugin is installed first
            mise plugins install $package 2>/dev/null || true
            mise use -g ${package}@${version}
            exit $?
        fi
        ;;
    npm)
        if command -v npm >/dev/null 2>&1; then
            echo -e "${CYAN}Installing npm package:${NC} ${BOLD}$package${NC}"
            npm install -g $package
            exit $?
        fi
        ;;
    mas)
        if command -v mas >/dev/null 2>&1; then
            echo -e "${CYAN}Installing Mac App Store app:${NC} ${BOLD}$package${NC}"
            mas install $package
            exit $?
        fi
        ;;
    vscode)
        if command -v code >/dev/null 2>&1; then
            echo -e "${CYAN}Installing VS Code extension:${NC} ${BOLD}$package${NC}"
            code --install-extension $package
            exit $?
        fi
        ;;
    cursor)
        if command -v cursor >/dev/null 2>&1; then
            echo -e "${CYAN}Installing Cursor extension:${NC} ${BOLD}$package${NC}"
            cursor --install-extension $package
            exit $?
        fi
        ;;
    uv)
        if command -v uv >/dev/null 2>&1; then
            echo -e "${CYAN}Installing Python tool:${NC} ${BOLD}$package${NC}"
            uv tool install $package
            exit $?
        fi
        ;;
    gcloud)
        if command -v gcloud >/dev/null 2>&1; then
            echo -e "${CYAN}Installing gcloud component:${NC} ${BOLD}$package${NC}"
            gcloud components install $package --quiet
            exit $?
        fi
        ;;
    *)
        echo -e "${RED}Unknown type:${NC} $type (entry: $1)"
        exit 1
        ;;
esac
